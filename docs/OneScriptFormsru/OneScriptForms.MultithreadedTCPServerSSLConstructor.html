<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<HTML>
<HEAD>
    <TITLE>MultithreadedTCPServerSSL Constructor</TITLE>
    <META content="text/html; charset=Windows-1252" http-equiv="Content-Type">
    <LINK rel="stylesheet" type="text/css" href="mainstyle.css">
    <script defer src="mobilstyle.js"></script>
</HEAD>
<BODY id=bodyID class=dtBODY>
    <DIV id=nsbanner>
        <DIV id=bannerrow1>
            <TABLE class=bannerparthead cellSpacing=0>
                <TBODY>
                    <TR id=hdr>
                        <TD class=runninghead></TD>
                        <TD class=product></TD>
                    </TR>
                </TBODY>
            </TABLE>
        </DIV>
        <DIV id=TitleRow>
            <H1 class=dtH1>МногопоточныйTCPСерверSSL (MultithreadedTCPServerSSL)&nbsp;Конструктор</H1>
        </DIV>
    </DIV>
    <DIV id=nstext>
        <H4 class=dtH4>Синтаксис</H4>
        <P>МногопоточныйTCPСерверSSL(&lt;НомерПорта&gt;, &lt;ПутьСертификата&gt;, &lt;Пароль&gt;)</P>
        <H4 class=dtH4>Параметры</H4>
        <DL>
            <DT><I>НомерПорта</I> (обязательный)</DT>
            <DD>Тип: Число.</DD>
            <DD>Прослушиваемый TCP-порт для входящих запросов на подключение на сервере.</DD>
            <DT><I>ПутьСертификата</I> (необязательный)</DT>
            <DD>Тип: Строка.</DD>
            <DD>Путь до файла сертификата <b>.pfx</b>. По умолчанию будет использован файл сертификата с именем <b>certificate.pfx</b>, лежащий в том же каталоге, что и запускаемый сценарий.</DD>
            <DT><I>Пароль</I> (необязательный)</DT>
            <DD>Тип: Строка.</DD>
            <DD>
                Пароль для сертификата. Здесь конечно нужно предварительно организовать запрос пароля, чтобы сохранить секретность пароля в контексте сценария. 
				По умолчанию в тестовых примерах будет использован пароль "swordfish20231223".
			</DD>
		</DL>
        <H4 class=dtH4>Описание</H4>
        <P>Создаёт новый экземпляр класса <B>МногопоточныйTCPСерверSSL&nbsp;(MultithreadedTCPServerSSL)</B>.</P>
        <H4 class=dtH4>Примечание</H4>
        <P></P>
        <H4 class=dtH4>Пример</H4>
        <P>
            <PRE class=code>
ТекПуть = Новый Файл(ТекущийСценарий().Источник).Путь;
Сервер = Ф.МногопоточныйTCPСерверSSL(8080, ОбъединитьПути(ТекПуть, "certificate.pfx"));
</PRE>
            <details>
                <summary>Полный пример кода</summary>
                <P>
                    <PRE class=code>
<button id="copy1" type="button" style="font-size:100%;">Копировать</button>
<hr style="border-color: lightgray;"><DIV id="cont1">
// Это пример запуска сервера с блокировкой сценария.
// В качестве клиента удобно будет использовать TCPКлиент (TCPClient)
Перем Сервер;

Процедура ПриПодключенииКлиента() Экспорт
	АргументыСобытия = Сервер.АргументыСобытия;
	Сообщить("Клиент подключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры

Процедура ВывестиСообщение(Данные, ИдентификаторКлиента)
	Попытка
		РазмерДанных = Данные.ПрочитатьЦелое32(0);
		ДанныеСообщения = Данные.Прочитать(4, РазмерДанных);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + РазмерДанных + " байт):" + Символы.ПС + ОтдельноеСообщение);
		Если (Данные.Размер - 4 - РазмерДанных) > 0 Тогда
			ОстатокДанных = Данные.Прочитать(4 + РазмерДанных, Данные.Размер - 4 - РазмерДанных);
			ВывестиСообщение(ОстатокДанных, ИдентификаторКлиента);
		КонецЕсли;
	Исключение
		ДанныеСообщения = Данные.Прочитать(0, Данные.Размер);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + Данные.Размер + " байт):" + Символы.ПС + ОтдельноеСообщение);
	КонецПопытки;
КонецПроцедуры
   
Процедура СерверПолучилДанные() Экспорт
	АргументыСобытия = Сервер.АргументыСобытия;
	Данные = АргументыСобытия.Данные;
	// Данные, полученные сервером, доступны нам как тип БуферДвоичныхДанных (BinaryDataBuffer).
	// Если с сервером будет взаимодействовать клиент, который формирует сообщения по оговоренным заранее правилам, 
	// появляется возможность делить данные на отдельные сообщения и кроме того знать тип данных отдельного сообщения.
	// Как частный случай можно на стороне клиента отправлять в сетевой поток сначала размер сообщения (четыре байта) 
	// а затем само сообщение. В этом примере так и сделано.
	// Разобъем полученные сервером данные на отдельные сообщения.
	ВывестиСообщение(Данные, АргументыСобытия.ИдентификаторКлиента);
	
	// Отправим всем строку.
	Сервер.ОтправитьВсем(ПолучитьСтрокуИзБуфераДвоичныхДанных(Данные));
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОтключенииКлиента() Экспорт
	АргументыСобытия = Сервер.АргументыСобытия;
	Сообщить("Клиент отключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОшибкеСервера() Экспорт
	АргументыСобытия = Сервер.АргументыСобытия;
	Сообщить("ОшибкаСервера: " + АргументыСобытия.ОшибкаСервера);
	Сообщить("========================");
КонецПроцедуры

ПодключитьВнешнююКомпоненту("C:\444\111\OneScriptForms\OneScriptForms\bin\Debug\OneScriptForms.dll");
Ф = Новый ФормыДляОдноСкрипта();

ТекПуть = Новый Файл(ТекущийСценарий().Источник).Путь;
Сервер = Ф.МногопоточныйTCPСерверSSL(8080, ОбъединитьПути(ТекПуть, "certificate.pfx"));
Сервер.ПроверятьАктивностьКлиента = Истина;
Сервер.ПриОшибкеСервера = Сервер.Действие(ЭтотОбъект, "ПриОшибкеСервера");
Сервер.ПриОтключенииКлиента = Сервер.Действие(ЭтотОбъект, "ПриОтключенииКлиента");
Сервер.СерверПолучилДанные = Сервер.Действие(ЭтотОбъект, "СерверПолучилДанные");
Сервер.ПриПодключенииКлиента = Сервер.Действие(ЭтотОбъект, "ПриПодключенииКлиента");
Сервер.Начать();

Пока Сервер.Продолжать Цикл
	Сервер.ПолучитьСобытие().Выполнить();
КонецЦикла;
</DIV>
</PRE>
            </details>
        <P></P>
        <H4 class=dtH4>Смотрите также</H4>
        <P>
            <A href="OneScriptForms.MultithreadedTCPServerSSL.html">МногопоточныйTCPСерверSSL&nbsp;(MultithreadedTCPServerSSL)&nbsp;Класс</A> | <A href="OneScriptForms.html">Библиотека&nbsp;OneScriptForms</A>
        </P>
    </DIV>
</BODY>
</HTML>
