<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<HTML>
<HEAD>
    <TITLE>ServerError Property</TITLE>
    <META content="text/html; charset=Windows-1252" http-equiv="Content-Type">
    <LINK rel="stylesheet" type="text/css" href="mainstyle.css">
    <script defer src="mobilstyle.js"></script>
</HEAD>
<BODY id=bodyID class=dtBODY onclick="divrightClick(event)">
    <DIV id=nsbanner>
        <DIV id=bannerrow1>
            <TABLE class=bannerparthead cellSpacing=0>
                <TBODY>
                    <TR id=hdr>
                        <TD class=runninghead></TD>
                        <TD class=product></TD>
                    </TR>
                </TBODY>
            </TABLE>
        </DIV>
        <DIV id=TitleRow>
            <H1 class=dtH1>СобытиеСервераАрг.ОшибкаСервера&nbsp;(ServerEventArgs.ServerError)&nbsp;Свойство</H1>
        </DIV>
    </DIV>
    <DIV id=nstext>
        <H4 class=dtH4>Использование</H4>
        <P>Только чтение.</P>
        <H4 class=dtH4>Значение</H4>
        <P>Тип: Строка.</P>
        <P>Возвращает информацию о возникшей на сервере ошибке или предупреждении.</P>
        <H4 class=dtH4>Примечание</H4>
        <P></P>
        <H4 class=dtH4>Пример</H4>
        <P>
            <PRE class=code>
Процедура ПриОшибкеСервера() Экспорт
	АргументыСобытия = Ф.АргументыСобытия;
	Сообщить("ОшибкаСервера: " + АргументыСобытия.ОшибкаСервера);
	Сообщить("========================");
КонецПроцедуры
</PRE>
            <details>
                <summary>Полный пример кода</summary>
                <P>
                    <PRE class=code>
<button id="copy1" type="button" style="font-size:100%;">Копировать</button>
<hr style="border-color: lightgray;"><DIV id="cont1">
// Это пример не блокирующего сценарий запуска сервера с построением формы для управления.
// В качестве клиента удобно будет использовать TCPКлиент (TCPClient)
Перем Ф, Сервер, ПолеВвода1;

Процедура ПриПодключенииКлиента() Экспорт
	АргументыСобытия = Ф.АргументыСобытия;
	Сообщить("Клиент подключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры

Процедура ВывестиСообщение(Данные, ИдентификаторКлиента)
	Попытка
		РазмерДанных = Данные.ПрочитатьЦелое32(0);
		ДанныеСообщения = Данные.Прочитать(4, РазмерДанных);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + РазмерДанных + " байт):" + Символы.ПС + ОтдельноеСообщение);
		Если (Данные.Размер - 4 - РазмерДанных) > 0 Тогда
			ОстатокДанных = Данные.Прочитать(4 + РазмерДанных, Данные.Размер - 4 - РазмерДанных);
			ВывестиСообщение(ОстатокДанных, ИдентификаторКлиента);
		КонецЕсли;
	Исключение
		ДанныеСообщения = Данные.Прочитать(0, Данные.Размер);
		ДД = ПолучитьДвоичныеДанныеИзБуфераДвоичныхДанных(ДанныеСообщения);
		ОтдельноеСообщение = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.UTF8);
		Сообщить("Сообщение от " + ИдентификаторКлиента + " (" + Данные.Размер + " байт):" + Символы.ПС + ОтдельноеСообщение);
	КонецПопытки;
КонецПроцедуры
   
Процедура СерверПолучилДанные() Экспорт
	АргументыСобытия = Ф.АргументыСобытия;
	Данные = АргументыСобытия.Данные;
	// Данные, полученные сервером, доступны нам как тип БуферДвоичныхДанных (BinaryDataBuffer).
	// Если с сервером будет взаимодействовать клиент, который формирует сообщения по оговоренным заранее правилам, 
	// появляется возможность делить данные на отдельные сообщения и кроме того знать тип данных отдельного сообщения.
	// Как частный случай можно на стороне клиента отправлять в сетевой поток сначала размер сообщения (четыре байта) 
	// а затем само сообщение. В этом примере так и сделано.
	// Разобъем полученные сервером данные на отдельные сообщения.
	ВывестиСообщение(Данные, АргументыСобытия.ИдентификаторКлиента);
	
	// Отправим всем строку.
	Сервер.ОтправитьВсем(ПолучитьСтрокуИзБуфераДвоичныхДанных(Данные));
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОтключенииКлиента() Экспорт
	АргументыСобытия = Ф.АргументыСобытия;
	Сообщить("Клиент отключился: " + АргументыСобытия.ИдентификаторКлиента);
	Сообщить("Активных клиентов: " + Сервер.КоличествоАктивныхКлиентов);
	Сообщить("========================");
КонецПроцедуры
   
Процедура ПриОшибкеСервера() Экспорт
	АргументыСобытия = Ф.АргументыСобытия;
	Сообщить("ОшибкаСервера: " + АргументыСобытия.ОшибкаСервера);
	Сообщить("========================");
КонецПроцедуры

Функция Кнопка1_Нажатие() Экспорт
	Сервер.Остановить();
КонецФункции

Функция Кнопка2_Нажатие() Экспорт
	Сервер.Начать();
КонецФункции

Функция Кнопка3_Нажатие() Экспорт
	Сервер.ОтправитьВсем(ПолеВвода1.Текст);
КонецФункции

ПодключитьВнешнююКомпоненту("C:\444\111\OneScriptForms\OneScriptForms\bin\Debug\OneScriptForms.dll");
Ф = Новый ФормыДляОдноСкрипта();
Форма1 = Ф.Форма();
Форма1.Отображать = Истина;
Форма1.Показать();
Форма1.Активизировать();

Кнопка1 = Форма1.ЭлементыУправления.Добавить(Ф.Кнопка());
Кнопка1.Нажатие = Ф.Действие(ЭтотОбъект, "Кнопка1_Нажатие");
Кнопка1.Текст = "Остановить";

Кнопка2 = Форма1.ЭлементыУправления.Добавить(Ф.Кнопка());
Кнопка2.Нажатие = Ф.Действие(ЭтотОбъект, "Кнопка2_Нажатие");
Кнопка2.Текст = "Запустить";
Кнопка2.Правее(Кнопка1, 10);

Кнопка3 = Форма1.ЭлементыУправления.Добавить(Ф.Кнопка());
Кнопка3.Нажатие = Ф.Действие(ЭтотОбъект, "Кнопка3_Нажатие");
Кнопка3.Ширина = 100;
Кнопка3.Текст = "Отправить всем";
Кнопка3.Правее(Кнопка2, 20);

ПолеВвода1 = Форма1.ЭлементыУправления.Добавить(Ф.ПолеВвода());
ПолеВвода1.Текст = "Всем клиентам от сервера.";
ПолеВвода1.Стыковка = Ф.СтильСтыковки.Низ;
ПолеВвода1.Высота = Форма1.КлиентВысота - 40;
ПолеВвода1.МногострочныйРежим = Истина;

Сервер = Ф.ФоновыйМногопоточныйСервер(8080);
Ф.ПриПодключенииКлиента = Ф.Действие(ЭтотОбъект, "ПриПодключенииКлиента");
Ф.ПриОтключенииКлиента = Ф.Действие(ЭтотОбъект, "ПриОтключенииКлиента");
Ф.ПриОшибкеСервера = Ф.Действие(ЭтотОбъект, "ПриОшибкеСервера");
Ф.СерверПолучилДанные = Ф.Действие(ЭтотОбъект, "СерверПолучилДанные");

Ф.ЗапуститьОбработкуСобытий();
</DIV>
</PRE>
            </details>
        <P></P>
        <H4 class=dtH4>Смотрите также</H4>
        <P>
            <A href="OneScriptForms.ServerEventArgs.html">СобытиеСервераАрг&nbsp;(ServerEventArgs)&nbsp;Класс</A> | <A href="OneScriptForms.html">Библиотека&nbsp;OneScriptForms</A>
        </P>
    </DIV>
</BODY>
</HTML>
